## 篩選條件3 : 涉及外幣之交易分錄

### 說明

 - 特徵 : 
    - 使用與央行或公開市場明顯不符的匯率，或是同一時期不同交易適用不同匯率，利用匯兌損益操縱財報
    - 利用外幣付款給實質不存在或關係密切之供應商以掩蓋資金挪用行為
 - 舉例 : 企業於年底將多筆美元應收帳款依高於市場的匯率進行重估，認列匯兌利益使盈餘增加，隔年初按實際市場匯率重估後沖回前期利益，認列匯兌損失。
 - 假設 : 
	1. 「匯率」欄位先篩值不為「0.00000」之所有分錄
	2. VB從明細表抓「傳票日期」、「原幣別」、「匯率」欄位，與台銀匯率交叉比對，判斷明細表所使用匯率是否合理
    3. 若匯率有差異，計算兩匯率下「傳票金額」差多少
    4. 根據審計員輸入之門檻值，判斷該差異是否超過門檻值
    5. 超過門檻值者，回報為異常分錄

### 1 ─ 爬蟲-台銀匯率

- **模組名稱** : `爬蟲-台銀匯率`
- **來源資料表** : `N/A`
- **輸出資料表** : `ExchangeRates`
- **目的說明** : 從台灣銀行網站抓取指定日期範圍內的每日即期匯率(美元、人民幣、日圓)，並儲存於資料表中，供後續比對使用。
- **模組語法** : 
```vb
Sub ScrapeExchangeRates()
    Dim xmlHttp As Object
    Dim html As Object
    Dim url As String
    Dim rows As Object
    Dim startDate As Date, endDate As Date
    Dim currentDate As Date
    Dim dateString As String
    Dim row As Object, cell As Object
    Dim usdRate As String, cnyRate As String, jpyRate As String
    Dim formattedDate As String
    Dim exists As Integer
    Dim insertSQL As String, updateSQL As String
    startDate = DateSerial(2024, 1, 1)
    endDate = DateSerial(2024, 12, 31)
    currentDate = startDate
    Do While currentDate <= endDate
        dateString = Format(currentDate, "yyyy-mm-dd")
        url = "https://rate.bot.com.tw/xrt/all/" & dateString
        Set xmlHttp = CreateObject("MSXML2.XMLHTTP")
        xmlHttp.Open "GET", url, False
        xmlHttp.setRequestHeader "User-Agent", "Mozilla/5.0"
        xmlHttp.setRequestHeader "Referer", "https://rate.bot.com.tw/xrt/all"
        xmlHttp.Send
        If xmlHttp.Status = 200 Then
            Set html = CreateObject("htmlfile")
            html.body.innerHTML = xmlHttp.responseText
            Set rows = html.getElementsByTagName("tr")
            usdRate = ""
            cnyRate = ""
            jpyRate = ""
            For Each row In rows
                If InStr(row.innerText, "美金 (USD)") > 0 Then
                    usdRate = Trim(row.getElementsByTagName("td")(3).innerText)
                ElseIf InStr(row.innerText, "人民幣 (CNY)") > 0 Then
                    cnyRate = Trim(row.getElementsByTagName("td")(3).innerText)
                ElseIf InStr(row.innerText, "日圓 (JPY)") > 0 Then
                    jpyRate = Trim(row.getElementsByTagName("td")(3).innerText)
                End If
            Next row
            formattedDate = "#" & Format(currentDate, "mm/dd/yyyy") & "#"
            exists = DCount("*", "ExchangeRates", "[Date] = " & formattedDate)
            If exists = 0 Then
                If IsNumeric(usdRate) Or IsNumeric(cnyRate) Or IsNumeric(jpyRate) Then
                    insertSQL = "INSERT INTO ExchangeRates ([Date], USD, CNY, JPY) VALUES (" & _
                                formattedDate & ", " & Nz(usdRate, 0) & ", " & Nz(cnyRate, 0) & ", " & Nz(jpyRate, 0) & ")"
                    Debug.Print "INSERT SQL: " & insertSQL
                    On Error GoTo InsertErr
                    DoCmd.SetWarnings False
                    DoCmd.RunSQL insertSQL
                    DoCmd.SetWarnings True
                    On Error GoTo 0
                End If
            Else
                updateSQL = "UPDATE ExchangeRates SET " & _
                            IIf(IsNumeric(usdRate), "USD = " & usdRate & ", ", "") & _
                            IIf(IsNumeric(cnyRate), "CNY = " & cnyRate & ", ", "") & _
                            IIf(IsNumeric(jpyRate), "JPY = " & jpyRate & ", ", "")
                If Right(updateSQL, 2) = ", " Then updateSQL = Left(updateSQL, Len(updateSQL) - 2)
                updateSQL = updateSQL & " WHERE [Date] = " & formattedDate

                Debug.Print "UPDATE SQL: " & updateSQL
                On Error GoTo InsertErr
                DoCmd.SetWarnings False
                DoCmd.RunSQL updateSQL
                DoCmd.SetWarnings True
                On Error GoTo 0
            End If
        Else
            Debug.Print "Failed to fetch data for " & dateString
        End If
        currentDate = currentDate + 1
    Loop
    MsgBox "資料已成功插入/更新 ExchangeRates！"
    Exit Sub
InsertErr:
    MsgBox "錯誤：" & Err.Description & vbCrLf & "SQL語句：" & insertSQL & updateSQL, vbCritical, "錯誤"
    Err.Clear
    Resume Next
End Sub
```

### 2 ─ 整合正確匯率至明細帳

- **查詢名稱** : `將正確匯率及明細帳統整為新表`
- **來源資料表** : `篩選條件3明細帳`, `ExchangeRates`
- **輸出資料表** : `ledger_mix_exchange_rate`
- **目的說明** : 將爬取的正確匯率資料，根據日期及幣別，整合至原始明細帳資料中，新增一欄顯示當日正確匯率。
- **查詢語法** : 
```sql
SELECT A.*, IIF(A.幣別='USD', E.USD, 
        IIF(A.幣別='CNY', E.CNY, 
            IIF(A.幣別='JPY', E.JPY, NULL))) AS CorrectRate INTO ledger_mix_exchange_rate
FROM 篩選條件3明細帳 AS A LEFT JOIN ExchangeRates AS E ON A.日期 = E.Date;
```

### 3 ─ 計算匯率差異金額及比率

- **查詢名稱** : `計算借貸方差異金額及比率`
- **來源資料表** : `ledger_mix_exchange_rate`
- **輸出資料表** : `different_amount_and_percentage`
- **目的說明** : 根據帳載匯率與正確匯率，計算每筆分錄的借方及貸方因匯率差異所產生的金額差異與比率差異。
- **查詢語法** : 
```sql
SELECT * INTO different_amount_and_percentage
FROM (SELECT A.*, ([匯率] - [CorrectRate]) * [原幣借方金額] AS 借方差異金額, ([匯率] - [CorrectRate]) * [原幣貸方金額] AS 貸方差異金額, ([本幣借方金額] / ([CorrectRate] * [原幣借方金額])) AS 借方差異比率, ([本幣貸方金額] / ([CorrectRate] * [原幣貸方金額])) AS 貸方差異比率 FROM ledger_mix_exchange_rate AS A)  AS B;
```

### 4 ─ 篩選匯率差異超標分錄

- **查詢名稱** : `再篩選差異金額超過門檻之列`
- **來源資料表** : `different_amount_and_percentage`
- **輸出資料表** : `abnormal_list`
- **目的說明** : 根據預設的差異金額與差異比率門檻值，篩選出匯率差異可能存在異常的交易分錄清單。
- **查詢語法** : 
```sql
SELECT A.傳票號碼, A.日期, A.匯率, A.CorrectRate, A.[借方差異金額], A.[貸方差異金額], A.[借方差異比率], A.[貸方差異比率] INTO abnormal_list
FROM (SELECT * FROM different_amount_and_percentage AS B WHERE B.[借方差異比率] > 1.1 OR  B.[貸方差異比率]>1.1)  AS A
WHERE A.[借方差異金額] > 7000000OR A.[貸方差異金額] > 7000000;
```

### 5 ─ 標記並列出所有匯率異常分錄

- **查詢名稱** : `併入明細帳並列出所有異常分錄`
- **來源資料表** : `different_amount_and_percentage`, `abnormal_list`
- **輸出資料表** : `all_abnormal_entry`
- **目的說明** : 將前一步篩選出的異常分錄清單，與包含差異計算的完整明細帳進行比對，標記出所有異常分錄並完整列出其詳細資訊。
- **查詢語法** : 
```sql
SELECT * INTO all_abnormal_entry
FROM (SELECT A.*, IIF(  A.[傳票號碼] IN (SELECT [傳票號碼]FROM abnormalEntries   ), 'V', '') AS 超過金額門檻之異常分錄 FROM different_amount_and_percentage AS A LEFT JOIN abnormal_list AS B ON (A.借方差異金額 = B.借方差異金額) AND (A.傳票號碼=B.傳票號碼))  AS T
WHERE T.超過金額門檻之異常分錄= 'V';
```

